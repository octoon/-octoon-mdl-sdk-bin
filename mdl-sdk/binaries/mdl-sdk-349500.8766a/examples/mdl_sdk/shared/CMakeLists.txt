#*****************************************************************************
# Copyright 2022 NVIDIA Corporation. All rights reserved.
#*****************************************************************************

# name of the target and the resulting library
set(PROJECT_NAME examples-mdl_sdk-shared)

# collect sources
set(PROJECT_SOURCES
    "example_cuda_shared.h"
    "example_shared.cpp"
    "example_shared.h"
    "compiled_material_traverser_base.h"
    "compiled_material_traverser_base.cpp"
    "gui/gui.h"
    "gui/gui.cpp"
    "gui/gui_material_properties.h"
    "gui/gui_material_properties.cpp"
    "lpe.cpp"
    "lpe.h"
    "texture_support_native.h"
    "utils/enums.h"
    "utils/io.h"
    "utils/mdl.h"
    "utils/os.h"
    "utils/strings.h"
    )

if(MDL_ENABLE_CUDA_EXAMPLES)
    set(PROJECT_SOURCES
        ${PROJECT_SOURCES}
        "texture_support_cuda.h"
        )
endif()

if(MDL_ENABLE_OPENGL_EXAMPLES)
    set(PROJECT_SOURCES
        ${PROJECT_SOURCES}
        "utils/gl_display.h"
        )
endif()

if(MDL_ENABLE_D3D11_EXAMPLES)
    set(PROJECT_SOURCES
        ${PROJECT_SOURCES}
        "gui/gui_api_interface_dx11.h"
        )
endif()

if(MDL_ENABLE_D3D12_EXAMPLES)
    set(PROJECT_SOURCES
        ${PROJECT_SOURCES}
        "gui/gui_api_interface_dx12.h"
        )
endif()

# create target from template
create_from_base_preset(
    TARGET ${PROJECT_NAME}
    NAMESPACE mdl_sdk
    OUTPUT_NAME "shared"
    SOURCES ${PROJECT_SOURCES}
    EXAMPLE
)
add_library(mdl_sdk::shared ALIAS ${PROJECT_NAME})

# inject the MDL_SAMPLES_ROOT path
target_compile_definitions(${PROJECT_NAME}
    PUBLIC
        "MDL_SAMPLES_ROOT=\"${CMAKE_CURRENT_SOURCE_DIR}/../..\""
    )

if(WINDOWS)
    # MS deprecated the codecvt_utf8 functions without providing a replacement
    # the deprecated function will be available until there is such a replacement
    target_compile_definitions(${PROJECT_NAME}
        PUBLIC
            "_SILENCE_CXX17_CODECVT_HEADER_DEPRECATION_WARNING"
        )
endif()

# add dependencies
target_add_dependencies(TARGET ${PROJECT_NAME}
    DEPENDS
        mdl::mdl_sdk
        imgui
    NO_RUNTIME_COPY     # header only -> the consumer will also add this dependency
    )

# make sure the plugins are build before samples are build
if(TARGET shaders-plugin-dds)
    add_dependencies(${PROJECT_NAME} shaders-plugin-dds)
endif()

if(TARGET shaders-plugin-freeimage)
    add_dependencies(${PROJECT_NAME} shaders-plugin-freeimage)
endif()

# add include directories
target_include_directories(${PROJECT_NAME}
    INTERFACE
        # the template makes non public headers private by default
        $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}> 
    )
